(library
  (complex-polar)
  (export install-polar-package)
  (import (rnrs)
          (base))
  (define (install-polar-package)
    (define (myreal-part z) (* (mymagnitude z) (cos (myangle z))))
    (define (myimag-part z) (* (mymagnitude z) (sin (myangle z))))
    (define (make-from-real-imag x y)
      (cons (sqrt (+ (square x) (square y)))
            (atan y x)))
    (define (mymagnitude z) (car z))
    (define (myangle z) (cdr z))
    (define (make-from-mag-ang r a)
      (cons r a))
    (define (tag x) (attach-tag 'polar x))
    (put 'real-part '(polar) myreal-part)
    (put 'imag-part '(polar) myimag-part)
    (put 'magnitude '(polar) mymagnitude)
    (put 'angle '(polar) myangle)
    (put 'make-from-real-imag 'polar
         (lambda (x y) (tag (make-from-real-imag x y))))
    (put 'make-from-mag-ang 'polar
         (lambda (r a) (tag (make-from-mag-ang r a))))
    'ok)
  )
